# Maintainer: Sutou Kouhei <kou@clear-code.com>

_realname=luarocks
pkgbase=mingw-w64-luajit-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-luajit-${_realname}")
pkgver=2.4.4
pkgrel=0
pkgdesc="the package manager for LuaJIT modules (mingw-w64)"
arch=('any')
url="https://luarocks.org"
license=("MIT")
install=luarocks-${CARCH}.install
depends=("${MINGW_PACKAGE_PREFIX}-luajit")
conflicts=("${MINGW_PACKAGE_PREFIX}-lua51-luarocks")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "unzip")
options=('staticlibs' 'strip')
source=("https://luarocks.org/releases/${_realname}-${pkgver}.tar.gz"
        "mingw32-msys2.patch")
sha256sums=('3938df33de33752ff2c526e604410af3dceb4b7ff06a770bc4a240de80a1f934'
            '14e55bcd507af2f374ac398f2741b6faddceb549f0fc15e6644a8f6c22c774b1')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/mingw32-msys2.patch
}

build() {
  #mkdir -p "build-${MINGW_CHOST}"
  cd "${srcdir}/${_realname}-${pkgver}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-lua-include=${MINGW_PREFIX}/include/luajit-2.0 \
    --lua-version=5.1 \
    --lua-suffix=jit.exe

  make build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
